plugins {
    id "java"
    id "net.tribe-seven.swig" version "0.1.1"
}


repositories {
    jcenter()
    maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version: '6.8.13'
}

sourceSets {
    main {
        resources {
            srcDir "$buildDir/lib"
        }
    }
}

// test {
//     environment "LD_LIBRARY_PATH": "$buildDir"
//     environment "DYLD_LIBRARY_PATH":  "$buildDir"
// }

task compileSwig(type: SwigTask) {
    description "Generate the JNI wrapping necessary files"
    verbose = true
    javaSourcesPath = new File("$projectDir/src/main/java/example_package")
    source = new File("$projectDir/swig/example.i")
    wrapperTargetFile = new File("$projectDir/build/example_wrap.c")
    module = "example"
    packageName = "example_package"
}

task compileObjects(dependsOn: compileSwig) {
    description "Compiling the objects"

    def JAVA_HOME = System.properties['java.home'] + "/.."

    buildDir.mkdirs()
    doLast {
        exec {
            commandLine "gcc", "-fpic", "-c", "c_src/example.c", "-o", "$buildDir/example.o", "-I${JAVA_HOME}/include/", "-I${JAVA_HOME}/include/linux"
        }

        exec {
            commandLine "gcc", "-fpic", "-c", "$buildDir/example_wrap.c", "-o", "$buildDir/example_wrap.o", "-I${JAVA_HOME}/include/", "-I${JAVA_HOME}/include/linux"
        }
    }
}

task generateLibrary(dependsOn: compileObjects) {
    description "Generate the shared library to be used"
    (new File("$buildDir/lib")).mkdirs()
    doLast {
        exec {
            commandLine "gcc", "-shared", "$buildDir/example.o",  "$buildDir/example_wrap.o", "-o", "$buildDir/lib/libexample.so"
        }
    }
}

compileJava.dependsOn generateLibrary
